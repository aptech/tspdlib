
/*
**> kpss_1br
**  Reference:
**  Kurozumi, E. (2002)
**  Testing for stationarity with a break,
**  Journal of Econometrics 108, 105-127.
**
**  Format:  { stat, tb, lambda, cv } = KPSS_1break(y, model, bwl, varm, trimm);
**           {PS,cv}              = STps(y, model, varm, tb);
**
**  Input:   Y	     -  Nx1 matrix, data,
**
**           model   - 	1 = Case 0 (Level shift);
**                      2 = Case 3 (Level and trend shift)
**
**           bwl     -  Optional, bandwidth for spectral window. 
**                      Default = round(4 * (T/100)^(2/9)).
**
**           varm    -  Optional, long-run consistent variance estimation method.
**                      Default = 1;
**                      1 = iid;
**                      2 = Bartlett
**                      3 = Quadratic Spectral (QS);
**                      4 = SPC with Bartlett /see (Sul, Phillips & Choi, 2005)
**                      5 = SPC with QS;
**                      6 = Kurozumi with Bartlett
**                      7 = Kurozumi with QS
**                      Updated from Carrion-i-Silvestre & Sanso (2007) GAUSS codes.
**                      See:
**                      Carrion-i-Silvestre, J. L., & Sanso, A. (2006).
**                      A guide to the computation of stationarity tests.
**                      Empirical Economics, 31(2), 433.
**
**  Output:  stat    - LM statistic with a break
**
**           PS      - PS statistic with a break
**
**			 tb      - break location
**
**           lambda  - break fraction (tb/T)
**
**			 cv      - 1%, 5%, 10% critical values for the break location
**
**  Author: Saban Nazlioglu
-- The author makes no performance guarantees.
-- for public non-commercial use only.
-- for any bugs, please send e-mail to snazlioglu@pau.edu.tr
**
*/

/*13 May 2016*/

proc(4) = KPSS_1break(y, model, ...);
    local t, ssr_min, tb1_min, KPSS_min;
    local t1, t2, tb1, dc, dt, du1, dt1, z;
    local ssr, KPSS, crit, TBi, bwl_dftl, l, varm, trimm, dt_vec, frac;
    
    T = rows(y);
    bwl_dftl = round(4 * (T/100)^(2/9));
    
    { l, varm, trimm } = dynargsGet(1|3, bwl_dftl, 1, 0.10);
       
    { dt_vec, y } =_checkfordate(y);
    
    ssr_min = 1e6;
    tb1_min = 0;
    KPSS_min = 0;
    
    T1 = round(trimm * rows(y));
    T2 = round((1 - trimm) * rows(y));

    // First break
    tb1 = t1;
    do until tb1 > t2;
        // Deterministic term=constant
        dc = ones(t, 1);
        
        // Deterministic term=trend
        dt = seqa(1, 1, t);
        if model == 1;
            du1 = zeros(tb1, 1)|ones(t - tb1, 1);
            z = dc~dt~du1;
        endif;
        
        if model == 2;
            du1 = zeros(tb1, 1)|ones(t - tb1, 1);
            dt1 = zeros(tb1, 1)|seqa(1, 1, (t - tb1));
            z = dc~dt~du1~dt1;
        endif;
        
        // KPSS statistic
        { kpss, ssr } = _get_kpss(y, z, varm, l, T, 0);
        
        // Global minimization
        if ssr < ssr_min;
            // Select minimum SSR
            ssr_min = ssr;
            
            // First break date
            tb1_min = tb1;
            
            // KPSS statistic with break
            KPSS_min = KPSS;
        endif;
        tb1 = tb1 + 1;
    endo;
    
    crit = zeros(5, 3);
    if model == 1;
        crit = {0.28299 0.37538 0.60388,
            0.22915 0.30212 0.48265,
            0.18678 0.24247 0.38052,
            0.16007 0.20106 0.30162,
            0.15176 0.18688 0.26842};
    endif;
    
    if model == 2;
        crit = {0.09724 0.12046 0.17704,
            0.09724 0.12046 0.14208,
            0.06485 0.07889 0.11308,
            0.05570 0.06615 0.09122,
            0.05267 0.06163 0.08216};
    endif;
    
    TBi = round(10 * (tb1_min/rows(y)));
    if TBi > 5;
        TBi = 10 - TBi;
    endif;

    _printHeaderTSPDLIB(0, varm, 2, 1, trimm, "KPSS", "Stationarity");
      
    frac = tb1_min/rows(y);
    
    if ismiss(dt_vec) != 1;
        tb1_min = dt_vec[tb1_min];
    endif;

    // Print results
    local top, side, stats, dec, colwidth;
    top = "KPSS Stat:";
    side = "";
    stats = KPSS_min;
    dec = 3;
    colwidth = 25;
    
    _printStatsTable(top, side, stats, rev(crit[TBi, .]'), dec, colwidth, 0);
    
    // Print break dates
    _printBreaks(dt_vec, tb1_min, frac, 0);
    
    _printConclusion(KPSS_min, rev(crit[TBi, .]'), 1, "stationarity");
    
    retp(KPSS_min, tb1_min, frac, rev(crit[TBi, .]'));
endp;

proc(2) = STps(y, model, varm, tb);
    local t, tt, dc, dt, du1, dt1, ystar, z;
    local b, e1, ssr, St, lrv, ps, cv, bwl, tb1;
    
    t = rows(y);
    tb1 = tb;
    ystar = zeros(t, 1);
    tt = 1;
    do while tt <= T;
        if tt <= tb;
            ystar[tt] = (tt/tb) * y[tt];
        endif;
        
        if tt > tb;
            ystar[tt] = (tt/(t-tb)) * y[tt];
        endif;
        tt = tt + 1;
    endo;
    
    // Deterministic term=constant
    dc = ones(t, 1);
    
    // Deterministic term=trend
    dt = seqa(1, 1, t);
    
    if model == 1;
        du1 = zeros(tb1, 1)|ones(t - tb1, 1);
        z = dc~dt~du1;
    endif;
    
    if model == 2;
        du1 = zeros(tb1, 1)|ones(t - tb1, 1);
        dt1 = zeros(tb1, 1)|seqa(1, 1, (t - tb1));
        z = dc~dt~du1~dt1;
    endif;
    
    // OLS estimation
    b = ystar/z;
    
    // Residuals
    e1 = ystar - z * b;
    
    // Sum of sqaured residuals
    ssr = e1'e1;
    
    // Partial sum of residuals
    St = cumsumc(e1);
    
    bwl = round(4 * (T/100)^(2/9));
    lrv = _get_lrv_type(varm, ssr, e1, bwl, 1);
    
    // STps statistic
    ps = T^(-2) * (St'St)/lrv;
    
    if model == 1;
        cv = 0.60704|0.74752|1.07366;
    endif;
    
    if model == 2;
        cv = 0.21067|0.24654|0.32862;
    endif;
    
    
    retp(ps, cv);
endp;

proc (2) = _get_kpss(y, z, varm, bwl, T, print_lrv);
    local b, e1, sig2, se, ssr, kpss, S, lrv;
    
    { b, e1, sig2, se, ssr } = myols(y, z);
    
    S = cumsumc(e1);                /*partial sum of residuals*/
    
    lrv = _get_lrv_type(varm, ssr, e1, bwl, print_lrv);
    
    // KPSS statistic
    kpss = T^(-2) * (S'S)/lrv;
    
    retp(kpss, ssr);
endp;
