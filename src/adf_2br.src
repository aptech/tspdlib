
/*
**> adf_2br
**  Reference:
**  Narayan, P. K. and Popp, S. (NP) (2010),
**  "A new unit root test with two structural breaks in level and slope at unknown time",
**  Journal of Applied Statistics 37, 1425-1438.
**
**  Forat:  { tstat, tb1, tb2, p, cv } = ADF_2break(y, model[, pmax, ic, trimm]);
**
**  Input:   Y	     -  Nx1 matrix, data,
**           model   - 	1=Model A: break in level
**                      2=Model C: break in level and trend
**           pmax    -  Optional, maximum number of lags for Dy; 0=no lags, Default = 8.
**           ic      -  Optional, information criterion. Default = 3.:
**                      1=Akaike
**                      2=Schwarz
**                      3=t-stat significance
**			 trimm   -  Optional, trimming rate
**                      Default 15% trimmage is defined as in ZA (1992) and LS (2003).
**
**  Output:  tstat   - minimum test statistic
**			 tb1     - first break location
**			 tb2     - second break location
**			 p       - number of lags selected by choosen information criterion
**			 cv      - 1%, 5%, 10% critical values for the chosen model
**
**  Author: Saban Nazlioglu
-- The author makes no performance guarantees.
-- for public non-commercial use only.
-- for any bugs, please send e-mail to snazlioglu@pau.edu.tr
**
*/

proc(5) = ADF_2breaks(y, model, ...);
    local ssrp, p_opt, t, tb, tb1, tb2, tb1_min, tb2_min, ADF_min, T1, T2, dc, dt, du1, du2, dt1, dt2, z;
    local taup, dy;
    local ly, stat;
    local cv, opt_lag, pmax, ic, trimm, dt_vec, frac;
    
    // Check for missings
    _checkForMissings(y, currentprocname());
    
    // Dynamic arguments
    { pmax, ic, trimm } = dynargsGet(1|3, 8, 3, 0.10);
    
    // Look for date variables
    { dt_vec, y } = _checkfordate(y);
    
    /*
    ** Set deterministic components
    */
    { t, dy, ly, dc, dt } = _getFourierDeterministic(y);
    
    t = rows(y);
    
    // Start counters 
    tb1_min = 0;
    tb2_min = 0;
    ADF_min = 1000;
    
    // Trim data
    T1 = round(trimm * t);
    T2 = round((1 - trimm) * t);
    t1 = maxc((3 + pmax)|ceil(trimm * t));
    t2 = minc((t - 3 -pmax)|floor((1 - trimm) * t));
    
    if T1 < pmax+2;
        T1 = pmax + 3;
    endif;
    
    tb1 = T1;
    
    do while tb1 <= T2;
        // Bounds as in LS
        if model == 1;
            tb2 = tb1 + 2;
        endif;    
        
        if model == 2;
            tb2 = tb1 + 3;
        endif;
        
        do while tb2 <= T2;
            
            if model == 1;
                du1 = zeros(tb1, 1)|ones(t - tb1, 1);
                du2 = zeros(tb2, 1)|ones(t - tb2, 1);
                dt1 = miss();
                dt2 = miss();
            endif;
            
            if model == 2;
                du1 = zeros(tb1, 1)|ones(t - tb1, 1);
                du2 = zeros(tb2, 1)|ones(t - tb2, 1);
                dt1 = zeros(tb1, 1)|seqa(1, 1, (t - tb1));
                dt2 = zeros(tb2, 1)|seqa(1, 1, (t - tb2));
            endif;
            
            // Get regression independent variables
            z = __get_break_z(dc, dt, du1, dt1, miss(), du2, dt2);
            
            // Select lags
            // This uses standard sample size to select lags
            // Then returns taup and ssrp for full sample 
            // size at optimal p
            { p_opt, taup, ssrp } = __lag_selection_loop(dy, pmax, ic, 1, ly, z);
            stat = taup;
            
            if stat < ADF_min;
                
                // First break date
                tb1_min = tb1;
                
                // Second break date
                tb2_min = tb2;  
                
                // T-statistic with breaks
                ADF_min = stat;  
                
                // Optimal lag
                opt_lag = p_opt; 
                
            endif;
            tb2 = tb2 + 1;
        endo;
        tb1 = tb1 + 1;
    endo;
    
    // Critical Values (see, Narayan & Popp, 2010, Table 3)
    if model == 1;
        if T <= 50;
            cv = -5.259|-4.514|-4.143;
        endif;
        
        if 50 < T and T <= 200;
            cv = -4.958|-4.316|-3.980;
        endif;
        
        if 200 < T and T <= 400;
            cv = -4.731|-4.136|-3.825;
        endif;
        
        if 400 < T;
            cv = -4.672|-4.081|-3.772;
        endif;
    endif;
    
    if model == 2;
        if T <= 50;
            cv = -5.949|-5.181|-4.789;
        endif;
        
        if 50 < T and T <= 200;
            cv = -5.576|-4.937|-4.596;
        endif;
        
        if 200 < T and T <= 400;
            cv = -5.318|-4.741|-4.430;
        endif;
        
        if 400 < T;
            cv = -5.287|-4.692|-4.396;
        endif;
    endif;
 
    /*
    ** Print results
    */
    _printHeaderTSPDLIB(ic, 0, 2, model, 2, trimm, "ADF", "Unit root", 0);
   
    // Convert breaks
    { frac, tb } = __convertbreaks(tb1_min|tb2_min, dt_vec, T);

    // Print results & Critical values
    local top, stats, dec, colwidth;
    top = "ADF-stat";
    stats = ADF_min;
    dec = 3;
    colwidth = 15;
    
    _printStatsTable("", top, stats, cv, dec, colwidth, 0, opt_lag-1);
       
    _printBreaks(dt_vec, tb, frac);
    
    // Print conclusion
    _printConclusion(ADF_min, cv, 0, "a unit root");
    
    retp(ADF_min, tb[1], tb[2], opt_lag-1, cv);
endp;
